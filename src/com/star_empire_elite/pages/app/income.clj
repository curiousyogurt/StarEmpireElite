;;;;;
;;;;; Income Phase - Resource Generation
;;;;;
;;;;; The income phase is the first phase of each turn where players collect resources from their
;;;;; planets and any other sources. Unlike later phases (expenses, exchange, building), the income
;;;;; phase requires no player decisions; it's purely informational, showing what resources will be
;;;;; generated before the player advances to the next phase.
;;;;;

(ns com.star-empire-elite.pages.app.income
  (:require [com.biffweb :as biff]
            [com.star-empire-elite.ui :as ui]
            [com.star-empire-elite.utils :as utils]
            [xtdb.api :as xt]))

;;;;
;;;; Calculations
;;;;

;;; Calculate income from all planet types using game constants. Returns a map of resource deltas,
;;; connecting each planet to the resources that is generating.
(defn calculate-income 
  "Calculate income from all planet types. Returns map of resource changes."
  [player game]
  ;; Keys such as :player/ore-planets are fully qualified keys in the player map.
  ;; That is, there is a key in the player map named :player/ore-planets, etc.
  ;;             (* resource count for player     resource value in game)
  {:ore-credits  (* (:player/ore-planets player)  (:game/ore-planet-credits game))
   :ore-fuel     (* (:player/ore-planets player)  (:game/ore-planet-fuel game))
   :ore-galaxars (* (:player/ore-planets player)  (:game/ore-planet-galaxars game))
   :food-food    (* (:player/food-planets player) (:game/food-planet-food game))
   :mil-soldiers (* (:player/mil-planets player)  (:game/mil-planet-soldiers game))
   :mil-fighters (* (:player/mil-planets player)  (:game/mil-planet-fighters game))
   :mil-stations (* (:player/mil-planets player)  (:game/mil-planet-stations game))
   :mil-agents   (* (:player/mil-planets player)  (:game/mil-planet-agents game))})

;;;;
;;;; UI Components
;;;;

(defn income-row
  "Renders an income row showing resources generated by a planet type.
  Uses custom mobile/desktop layouts optimized for income display:
  - Mobile: Compact grid showing planet type, count, and top resources
  - Desktop: Full table row with all columns, zero values dimmed at 20% opacity

  Parameters:
  - planet-type-name: Display name (e.g. 'Ore', 'Food', 'Mil')
  - planet-count: Number of this planet type the player owns
  - income-map: Map with resource keys and their values (e.g. {:credits 500 :fuel 200})"
  [planet-type-name planet-count income-map]
  [:div.border-b.border-green-400.last:border-b-0

   ;; Mobile: Compact grid with narrower planet type column
   [:div.grid.gap-1.px-2.py-2.items-center.text-xs.leading-tight.lg:hidden
    {:style {:grid-template-columns "0.5fr 0.4fr 4fr"}}  ; Reduced first two columns, expanded last
    
    ;; Planet type
    [:div.font-mono planet-type-name]
    
    ;; Planet count
    [:div.text.font-mono (str "(" planet-count ")")]
    
    ;; Resources - all in one column, spaced out
    [:div.flex.gap-3.justify-start.flex-wrap
     (for [[k label] [[:credits "Crds"] [:fuel "Fuel"] [:galaxars "Glxrs"] 
                      [:food "Food"] [:soldiers "Sold"] [:fighters "Fghts"] 
                      [:stations "Stns"] [:agents "Agnts"]]
           :let [v (or (k income-map) 0)]
           :when (pos? v)]
       [:div.whitespace-nowrap {:key k}
        [:span.text-green-300 label]
        [:span.font-mono.ml-1 "+" (ui/format-number v)]])]]

   ;; Desktop: Full table row (unchanged)
   [:div.hidden.lg:grid.lg:gap-4.lg:items-center.lg:px-4.lg:py-3
    {:style {:grid-template-columns "repeat(10, minmax(0, 1fr))"}}
    [:div planet-type-name]
    [:div planet-count]
    (for [k [:credits :fuel :galaxars :food :soldiers :fighters :stations :agents]
          :let [v (or (k income-map) 0)]]
      [:div {:key k}
       [:span.font-mono {:style {:word-spacing "-0.2em"}
                         :class (when (zero? v) "opacity-20")}
        "+" (ui/format-number v)]])]])

;;;;
;;;; Actions
;;;;
;;;; There are two parts to the actions for this phase: (i) income-page, which shows the income due 
;;;; the player in the current round, and (ii) apply-income which shows the income and then applies it
;;;; when the user advances to the next page.  Both functions call (calculate-income player game) in
;;;; order to determine income from all the planet types, making reference to the game constants.
;;;;

;;; Applies income from all planet types and advances to the next phase. Uses calculate-income to
;;; determine resource changes, and then commits them in a single transaction.
(defn apply-income [{:keys [path-params biff/db] :as ctx}]
  (utils/with-player-and-game [player game player-id] ctx
    ;; Phase validation prevents players from applying income multiple times or out of order
    (if-let [redirect (utils/validate-phase player 1 player-id)]
      redirect
      (let [income (calculate-income player game)]
        ;; Single atomic transaction updates resources and advances the phase all at once. This
        ;; prevents partial updates if something fails midway through.
        (biff/submit-tx ctx      ; Submit a db transaction using Biff framework
                        [{:db/doc-type :player ; Document type is player
                          :db/op :update       ; Update operation
                          :xt/id player-id     ; Target has id player-id
                          :player/credits  (+ (:player/credits player)  (:ore-credits income))
                          :player/food     (+ (:player/food player)     (:food-food income))
                          :player/fuel     (+ (:player/fuel player)     (:ore-fuel income))
                          :player/galaxars (+ (:player/galaxars player) (:ore-galaxars income))
                          :player/soldiers (+ (:player/soldiers player) (:mil-soldiers income))
                          :player/fighters (+ (:player/fighters player) (:mil-fighters income))
                          :player/stations (+ (:player/stations player) (:mil-stations income))
                          :player/agents   (+ (:player/agents player)   (:mil-agents income))
                          :player/current-phase 2}]) ; Move player to next phase
        ;; Set 303 status, which tells the browser to redirect to the expenses page
        {:status 303
         :headers {"location" (str "/app/game/" player-id "/expenses")}}))))

;;; Shows preview of income generation before committing. Unlike expenses/exchange/building phases,
;;; no htmx dynamic updates are needed, since there are no player choices to validate.
(defn income-page [{:keys [player game]}]
  (let [income (calculate-income player game)]
    (ui/page
      {}
      [:div.text-green-400.font-mono
       [:h1.text-3xl.font-bold.mb-6 (:player/empire-name player)]

       ;;; Current phase display:
       (ui/phase-header (:player/current-phase player) "INCOME")

       ;;; Current resources display:
       ;;; Shows starting position before income is applied, helping players understand what they have
       ;;; available for the upcoming expenses phase.
       (ui/resource-display-grid player "Resources Before Income")

       ;;; Income breakdown table:
       ;;; Displays as cards on mobile and as a spreadsheet-like table on wide screens.
       ;;; This makes it clear which planets generate which resources.
       [:h3.font-bold.mb-4 "Income"]
       [:div.border.border-green-400.mb-8
        ;; Header row (only visible on wide screens)
        (ui/phase-table-header ["Planet"   "Count"    "Credits" "Fuel" "Galaxars" "Food" "Soldiers"
                                "Fighters" "Stations" "Agents"])

        ;; Ore planets generate economic resources: credits, fuel, and galaxars (the premium currency)
        (income-row "Ore" 
                    (:player/ore-planets player)
                    {:credits (:ore-credits income)
                     :fuel (:ore-fuel income)
                     :galaxars (:ore-galaxars income)})

        ;; Food planets have a single output: food for feeding the population and the military
        (income-row "Food"
                    (:player/food-planets player)
                    {:food (:food-food income)})

        ;; Military planets generate military units: soldiers, fighters, stations, and agents
        (income-row "Mil"
                    (:player/mil-planets player)
                    {:soldiers (:mil-soldiers income)
                     :fighters (:mil-fighters income)
                     :stations (:mil-stations income)
                     :agents (:mil-agents income)})]

       ;;; Final resource totals:
       ;;; Shows what the player will have after income is applied. This preview helps players
       ;;; anticipate actions that they might want or need to take in the subsequent phases.
       (ui/resource-display-grid 
         {:credits  (+ (:player/credits player)  (:ore-credits income))
          :food     (+ (:player/food player)     (:food-food income))
          :fuel     (+ (:player/fuel player)     (:ore-fuel income))
          :galaxars (+ (:player/galaxars player) (:ore-galaxars income))
          :soldiers (+ (:player/soldiers player) (:mil-soldiers income))
          :fighters (+ (:player/fighters player) (:mil-fighters income))
          :stations (+ (:player/stations player) (:mil-stations income))
          :agents   (+ (:player/agents player)   (:mil-agents income))}
         "Resources After Income")

       [:.h-6]

       ;;; Phase Advance Form:
       ;;; Simple form submission with no validation needed. Income is always valid and beneficial.
       ;;; No htmx dynamic updates (unlike later phases) since there are no player choices to 
       ;;; validate.
       (biff/form
         {:action (str "/app/game/" (:xt/id player) "/apply-income") :method "post"}
         [:button.bg-green-400.text-black.px-6.py-2.font-bold.hover:bg-green-300.transition-colors
          {:type "submit"} "Continue to Expenses"])])))

